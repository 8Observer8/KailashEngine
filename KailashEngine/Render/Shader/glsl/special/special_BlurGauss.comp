
layout (local_size_x = 1024) in;


uniform sampler2D sampler0;				// Source Texture
writeonly uniform image2D sampler1;		// Destination Texture


shared vec3 cache[gl_WorkGroupSize.x * 2];


uniform int direction_selector;
uniform int blur_amount;
uniform vec2 texture_size;


void main() 
{
	uint id = gl_LocalInvocationID.x;

	ivec2 texCoord_1 = ivec2(id * 2, gl_WorkGroupID.x);
	ivec2 texCoord_2 = ivec2(id * 2 + 1, gl_WorkGroupID.x);

	// Swizzle with uniform so we don't have to branch
	texCoord_1 = ivec2(texCoord_1[direction_selector], texCoord_1[abs(direction_selector-1)]);
	texCoord_2 = ivec2(texCoord_2[direction_selector], texCoord_2[abs(direction_selector-1)]);
	int texCoord_index_1 = texCoord_1[direction_selector];
	int texCoord_index_2 = texCoord_2[direction_selector];
	float texture_size_early_out = texture_size[direction_selector];

	cache[id * 2] = texelFetch(sampler0, texCoord_1, 0).xyz;
	cache[id * 2 + 1] = texelFetch(sampler0, texCoord_2, 0).xyz;
	
	barrier();
	memoryBarrierShared();

	if(id * 2 > texture_size_early_out)
	{
		imageStore(sampler1, texCoord_1, vec4(0.0));
		imageStore(sampler1, texCoord_2, vec4(0.0));
		return;
	}
	/*
	else
	{
		imageStore(sampler1, texCoord_1, vec4(texCoord_1 / texture_size, 0.0, 1.0));
		//imageStore(sampler1, texCoord_2, vec4(texCoord_2 / texture_size, 0.0, 1.0));
		return;	
	}
	*/

	
	float SIGMA = float(blur_amount) / 9.7;
	float SIGMA_2 = SIGMA * SIGMA;

	vec3 guass_increment;
	guass_increment.x = 1.0 / (sqrt(MATH_2_PI) * SIGMA);
	guass_increment.y = exp(-0.5 / SIGMA_2);
	guass_increment.z = guass_increment.y * guass_increment.y;
	
	vec3 output_1 = cache[texCoord_index_1] * guass_increment.x;
	vec3 output_2 = cache[texCoord_index_2] * guass_increment.x;

	float increment_sum = guass_increment.x;
	guass_increment.xy *= guass_increment.yz;


	for (int i = 1; i < blur_amount; i++)
	{
		int index = i;

		output_1 += cache[texCoord_index_1 + index] * guass_increment.x;
		output_1 += cache[texCoord_index_1 - index] * guass_increment.x;

		output_2 += cache[texCoord_index_2 + index] * guass_increment.x;
		output_2 += cache[texCoord_index_2 - index] * guass_increment.x;

		increment_sum += 2.0 * guass_increment.x;
		guass_increment.xy *= guass_increment.yz;
	}

	output_1 /= increment_sum;
	output_2 /= increment_sum;

	imageStore(sampler1, texCoord_1, vec4(output_1, 1.0));
	imageStore(sampler1, texCoord_2, vec4(output_2, 1.0));
}
